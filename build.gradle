plugins {
    id 'java'
    id "java-library"
    id 'maven-publish'
    id "com.github.johnrengelman.shadow" version "6.1.0"
    id "io.freefair.lombok" version "6.1.0"
}

group = 'dev.aseef'
version = '1.0.0-ALPHA-1'

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        name = 'velocity'
        url = 'https://repo.velocitypowered.com/snapshots/'
    }
}

dependencies {
    compileOnly ('org.spigotmc:spigot-api:1.12.2-R0.1-SNAPSHOT')
    compileOnly ('com.velocitypowered:velocity-api:3.0.1')
    compileOnly ('net.md-5:bungeecord-api:1.18-R0.1-SNAPSHOT')

    implementation ('org.apache.logging.log4j:log4j-core:2.17.2') {
        because 'Provides access to the most popular Java logging framework: Log4J'
    }
    implementation ('org.objenesis:objenesis:3.2') {
        because 'Objenesis allows us to create \'empty\' instances of classes [useful for serialization purposes]'
    }
    implementation ('com.zaxxer:HikariCP:4.0.3') {
        because 'HikariCP is one of the best SQL pool managers out there.'
    }
    implementation ('redis.clients:jedis:4.2.3') {
        because 'Jedis provides an API for Redis interactions'
    }
    implementation ('mysql:mysql-connector-java:8.0.29') {
        because 'Java\'s MySQL connector provides the drivers for MySQL/MariaDB databases'
    }
    implementation ('org.postgresql:postgresql:42.3.5') {
        because 'Provides access to the PostgreSQL drivers'
    }
    implementation ('org.mongodb:mongodb-driver-sync:4.6.0') {
        because 'Provides access to the MongoDB drivers'
    }
    implementation ('com.h2database:h2:2.1.212') {
        because 'Provides access to the H2 drivers'
    }

    // so others can access BSON since its part of the API
    api ('org.mongodb:bson:4.6.0')
}

def targetJavaVersion = 8
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [
            version: version,
            author: "Aseef"
    ]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching(['plugin.yml', 'velocity-plugin.json', 'bungee.yml']) {
        expand props
    }
}
